apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

group = "com.btmatthews.springboot"
archivesBaseName = "memcached-spring-boot-starter"
version = "1.1.1-SNAPSHOT"

dependencies {
    compile('org.springframework.boot:spring-boot:1.5.6.RELEASE')
    compile('net.spy:spymemcached:2.12.0')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Memcached Spring Boot Starter'
                packaging 'jar'
                description 'A Spring Boot Starter to configure a Memcached client'
                url 'https://github.com/bmatthews68/memcached-spring-boot-starter'

                scm {
                    connection 'scm:git:git@github.com:bmatthews68/memcached-spring-boot-starter.git'
                    developerConnection 'scm:git:git@github.com:bmatthews68/memcached-spring-boot-starter.git'
                    url 'https://github.com/bmatthews68/memcached-spring-boot-starter'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'bmatthews68'
                        name 'Brian Matthews'
                        email 'brian@btmatthews.com'
                    }
                }
            }
        }
    }
}